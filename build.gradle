plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'org.spongepowered.plugin' version '0.6'
}

def semver = '0.20.0'
def buildNum = System.getenv('BUILD_NUMBER') ?: null

group = 'net.mohron.skyclaims'
version = (buildNum == null) ? semver + '-beta' : semver + '-' + buildNum + '-dev'
description = 'SkyClaims is an Island Management plugin that integrates with GriefPrevention.'

repositories {
    jcenter()
    maven {
        name = 'Sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'Forge'
        url = 'http://files.minecraftforge.net/maven'
    }
    maven {
        name = 'Nucleus'
        url = 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

def griefPrevention = 'me.ryanhamshire:griefprevention:1.10.2-2.3.1.292:api'
def nucleus = 'io.github.nucleuspowered:nucleus-api:0.26.0-S5.1'

dependencies {
    compile('org.spongepowered:spongeapi:5.1.0')
    compileOnly('org.spongepowered:spongeforge:1.10.2-2254-5.2.0-BETA-2255')
    compile(griefPrevention)
    compile(nucleus)
}

shadowJar {
    dependencies {
        include(dependency(griefPrevention))
        include(dependency(nucleus))
    }
}

blossom {
    def pluginInfo = 'src/main/java/net/mohron/skyclaims/PluginInfo.java'

    replaceToken '@NAME@', project.name, pluginInfo
    replaceToken '@VERSION@', project.version, pluginInfo
    replaceToken '@DESCRIPTION@', project.description, pluginInfo
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        sign configurations.archives
    }
}

build.dependsOn(shadowJar)