plugins {
    id "java"
    id "idea"
    id "signing"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.qixalite.spongestart2" version "4.0.0"
    id "io.franzbecker.gradle-lombok" version "1.14"
    id "org.sonarqube" version "2.6"
}

group = "net.mohron.skyclaims"
version = "${major}.${minor}.${patch}-${api}-${suffix}"
description = "SkyClaims is an Island Management plugin that integrates with GriefPrevention."

if (System.getenv().BUILD_NUMBER != null) {
    version += "-${System.getenv().BUILD_NUMBER}"
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    maven {
        name = "Sponge"
        url = "http://repo.spongepowered.org/maven"
    }
    maven {
        name = "Forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "Nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven {
        name = "LuckPerms"
        url = "https://repo.lucko.me/"
    }
    maven {
        name = "bStats"
        url = "http://repo.bstats.org/content/repositories/releases/"
    }
}

dependencies {
    compile("org.spongepowered:spongeapi:${spongeapi}")
    compile("me.ryanhamshire:griefprevention:${griefprevention}:api")
    compile("io.github.nucleuspowered:nucleus-api:${nucleus}")
    compile("org.bstats:bstats-sponge:${bstats}")
    runtimeOnly("me.ryanhamshire:griefprevention:${griefprevention}")
    //compile fileTree(dir: "libs", include: "*.jar")
}

lombok {
    version = "1.18.4"
    sha256 = ""
}

shadowJar {
    classifier = "plugin"
    dependencies {
        include(dependency("org.bstats:bstats-sponge:${bstats}"))
    }
    minimize()
}

blossom {
    def pluginInfo = "src/main/java/net/mohron/skyclaims/PluginInfo.java"

    replaceToken "@NAME@", project.name, pluginInfo
    replaceToken "@VERSION@", project.version, pluginInfo
    replaceToken "@DESCRIPTION@", project.description, pluginInfo
}

spongestart {
    online = "false"
    minecraft = "1.12.2"
}

signing {
    required {Project.hasProperty("signing.keyId")}
    sign configurations.archives, configurations.shadow
}

build.dependsOn(shadowJar)