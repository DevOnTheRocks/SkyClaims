plugins {
    id "java"
    id "idea"
    id "signing"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "io.franzbecker.gradle-lombok" version "3.1.0"
    id "org.sonarqube" version "2.6"
    id "org.spongepowered.plugin" version "0.9.0"
}

group = "net.mohron.skyclaims"
version = "${major}.${minor}.${patch}-${api}-${suffix}"
description = "SkyClaims is an Island Management plugin that integrates with GriefDefender."

if (System.getenv().BUILD_NUMBER != null) {
    version += "-${System.getenv().BUILD_NUMBER}"
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = "Forge"
        url = "https://files.minecraftforge.net/maven"
    }
    maven {
        name = "GriefDefender"
        url = "https://repo.glaremasters.me/repository/bloodshot"
    }
    maven {
        name = "Nucleus"
        url = "https://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven {
        name = "LuckPerms"
        url = "https://repo.lucko.me/"
    }
    maven {
        name = "Aikar"
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    annotationProcessor("org.spongepowered:spongeapi:${spongeapi}")
    implementation("org.spongepowered:spongeapi:${spongeapi}")
    implementation("com.griefdefender:api:2.0.0-SNAPSHOT")
    implementation("me.lucko.luckperms:luckperms-api:4.3")
    implementation("net.kyori:adventure-api:4.8.0")
    implementation("net.kyori:adventure-platform-spongeapi:4.0.0-SNAPSHOT")
    implementation("net.kyori:adventure-text-serializer-spongeapi:4.0.0-SNAPSHOT")
    implementation("net.kyori:event-api:5.0.0-SNAPSHOT")
    implementation("co.aikar:acf-sponge:0.5.0-SNAPSHOT")
    implementation("io.github.nucleuspowered:nucleus-api:${nucleus}")
    implementation("org.bstats:bstats-sponge:${bstats}")
}

lombok {
    version = "1.18.8"
    sha256 = ""
}

shadowJar {
    archiveClassifier.set("plugin")
    dependencies {
        include(dependency("org.bstats:bstats-base:${bstats}"))
        include(dependency("org.bstats:bstats-sponge:${bstats}"))
    }
    relocate "org.bstats", "net.mohron.skyclaims.metrics"

    relocate("net.kyori", "com.griefdefender.lib.kyori")
    relocate("co.aikar.commands", "com.griefdefender.lib.aikar.commands")

    minimize()
}

blossom {
    def pluginInfo = "src/main/java/net/mohron/skyclaims/PluginInfo.java"

    replaceToken "@NAME@", project.name, pluginInfo
    replaceToken "@VERSION@", project.version, pluginInfo
    replaceToken "@DESCRIPTION@", project.description, pluginInfo
    replaceToken "@SPONGE_API@", spongeapi, pluginInfo
    replaceToken "@GRIEF_DEFENDER@", griefdefender, pluginInfo
    replaceToken "@NUCLEUS@", nucleus, pluginInfo
}

task cleanModsFolder(type: Delete) {
    delete fileTree("run/forge/mods").matching {
        include "**/SkyClaims-*-plugin.jar"
    }
}

task copyPlugin(type: Copy) {
    from 'build/libs'
    into 'run/forge/mods'
    include "SkyClaims-${version}-plugin.jar"
}
copyPlugin.dependsOn build
copyPlugin.dependsOn cleanModsFolder

signing {
    required {Project.hasProperty("signing.keyId")}
    sign configurations.archives, configurations.shadow
}

build.dependsOn(shadowJar)