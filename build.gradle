plugins {
    id 'java'
    id 'idea'
    id 'signing'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'org.spongepowered.plugin' version '0.6'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def major = "0"
def minor = "18"
def patch = "6"
def semver = major + "." + minor + "." + patch
def isJenkins = System.getenv("BUILD_NUMBER") != null
def suffix = isJenkins ? "-dev" : "-beta"

group = 'net.mohron.skyclaims'
version = semver + suffix
description = 'SkyClaims is an Island Management plugin that integrates with GriefPrevention.'

if (isJenkins) {
    version = semver + "-" + System.getenv("BUILD_NUMBER") + suffix
}

repositories {
    jcenter()
    maven {
        name = 'Sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'Forge'
        url = 'http://files.minecraftforge.net/maven'
    }
    maven {
        name = 'Nucleus'
        url = 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
}

dependencies {
    compile("org.spongepowered:spongeapi:5.0.0")
    compile("me.ryanhamshire:griefprevention:1.10.2-2.3.1.260")
    compile("io.github.nucleuspowered:nucleus-api:0.24.1-S5.1")
}

blossom {
    def pluginInfo = 'src/main/java/net/mohron/skyclaims/PluginInfo.java'

    replaceToken '@NAME@', project.name, pluginInfo
    replaceToken '@VERSION@', semver + suffix, pluginInfo
    replaceToken '@DESCRIPTION@', project.description, pluginInfo
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        sign configurations.archives
    }
}